_dimData = 
VAR MenorDia =
    MIN ( FactDrop[CI_CREATED_DATE] )

VAR MaiorDia =
    MAX ( FactDrop[CI_CREATED_DATE] )

VAR BaseCalendar =
    CALENDAR ( MenorDia, MaiorDia )

RETURN

    GENERATE (
        BaseCalendar,
        VAR BaseDate = [Date]
        VAR DateKey = 
            VALUE ( FORMAT ( BaseDate, "YYYYMMDD" ) )
        VAR Dia = 
            FORMAT (
                DAY( BaseDate ),
            "00" )
        VAR Ano = 
            YEAR ( BaseDate )
        VAR MesNumero = 
            MONTH ( BaseDate )
        VAR MesNome = 
            CONCATENATE (
                        UPPER (
                            LEFT (
                                FORMAT ( BaseDate, "mmm" ),
                            1 )
                        ),
                        MID (
                            FORMAT ( BaseDate, "mmm" ), 2, 2
                        )
                    )
        VAR MesAno = 
                CONCATENATE (
                        MesNome,
                        FORMAT ( BaseDate, " yy" )
                    )
        VAR Periodo = 
            IF (
                YEAR ( BaseDate ) = YEAR ( TODAY () )
                && MONTH ( BaseDate ) = MONTH ( TODAY () ),
                "Mês Atual",
                IF(
                    YEAR ( BaseDate ) = YEAR ( EDATE( TODAY(), -1 ) )
                    && MONTH ( EDATE( TODAY(), -1 ) ) = MesNumero,
                    "Mês Anterior",
                    "Demais Meses"
                )
            )
        VAR Semana = 
            "Sem " & FORMAT (
                WEEKNUM ( BaseDate, 1 ),
            "00" )
        RETURN ROW (
            "DateKey", DateKey,
            "Dia", Dia,
            "Mes", MesNumero,
            "Ano", Ano,
            "Mes_nome", MesNome,
            "Ano e Mes", MesAno,
            "Período", Periodo,
            "Semana", Semana
        )
    )
